import java.text.SimpleDateFormat
import java.util.Date

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE'
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.6.6'
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'org.springframework.boot' version '2.6.6'
    id 'com.bmuschko.docker-remote-api' version '9.4.0'
}

apply plugin: 'com.bmuschko.docker-remote-api'
apply plugin: 'io.spring.dependency-management'

import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

configurations {
    // A configuration meant for consumers that need the API of this component
    implementation {
        canBeResolved = true
    }
}

repositories {
    mavenCentral()
}

sourceCompatibility = '11'
targetCompatibility = '11'

// This affects the Docker repo name.
group = 'extender'

dependencyManagement {
    imports {
        mavenBom 'com.amazonaws:aws-java-sdk-bom:1.12.129'
    }
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude module: 'spring-boot-starter-tomcat'
    }

    implementation('com.googlecode.json-simple:json-simple:1.1.1') {
        exclude module: 'junit'
    }
    implementation('com.amazonaws:aws-java-sdk-s3:1.12.180')
    implementation('com.google.guava:guava:30.0-jre')
    implementation('com.samskivert:jmustache:1.15')
    implementation('commons-chain:commons-chain:1.2')
    implementation('commons-fileupload:commons-fileupload:1.3.3')
    implementation('commons-io:commons-io:2.11.0')
    implementation('io.micrometer:micrometer-core:1.8.1')
    implementation('org.apache.commons:commons-compress:1.21')
    implementation('org.apache.commons:commons-lang3:3.8.1')
    implementation('org.apache.httpcomponents:httpclient:4.5.13')
    implementation('org.apache.httpcomponents:httpmime:4.5.13')
    implementation('org.influxdb:influxdb-java:2.10')
    implementation('org.springframework.boot:spring-boot-starter-actuator')
    implementation('org.springframework.boot:spring-boot-starter-jetty')
    implementation('org.springframework.boot:spring-boot-starter-security')
    implementation('org.springframework.security:spring-security-test')
    implementation('io.micrometer:micrometer-core:1.8.1')
    runtimeOnly('io.micrometer:micrometer-registry-influx:1.8.1')
    runtimeOnly('io.micrometer:micrometer-registry-prometheus:1.8.3')
    runtimeOnly('net.logstash.logback:logstash-logback-encoder:7.0.1')

    testImplementation('junit:junit:4.12')
    testImplementation('org.springframework:spring-test')
    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('org.smali:dexlib2:2.2.5')
    testImplementation project(':client')
}

task createVersionFile() {
    def sha = System.getenv("COMMIT_SHORT_SHA")
    if (sha == null) {
        def outputStream = new ByteArrayOutputStream()
        exec {
            commandLine "git", "rev-parse", "--short", "HEAD"
            standardOutput = outputStream
        }
        sha = outputStream.toString().trim()
    }
    def build_time = new SimpleDateFormat("yyyy-MM-dd HH:mm:ss").format(new Date ())
    def text = """
// Auto generated by gradle

package com.defold.extender;
public class Version {
    public static final String gitVersion = "${sha}";
    public static final String buildTime = "${build_time}";
}
"""
    def binding = ["sha" : sha, "build_time" : build_time]

    def engine = new groovy.text.SimpleTemplateEngine()
    def template = engine.createTemplate(text).make(binding)

    new File(projectDir.toString() + "/src/main/java/com/defold/extender", "Version.java").text = template.toString()
    project.ext.set("outSha", sha)
}

compileJava {
    dependsOn createVersionFile
}

// Creates the full jar file, with the extender server classes included
bootJar {
    archiveBaseName = 'extender'
    version =  '0.1.0'
}

// Builds the extender server base classes into a .jar file
jar {
    archiveBaseName = 'extender-base'
    version =  '0.1.0'
    archiveClassifier = '' // use empty string to avoid the 'plain' suffix
}

File dockerBuildDir = new File(project.buildDir, 'docker-stage');

task dockerStageDir {
    doFirst {
        if (dockerBuildDir.exists()) {
            dockerBuildDir.delete();
        }
        dockerBuildDir.mkdirs();
        copy {
            from(project.projectDir) {
                include "users/**"
            }
            into dockerBuildDir
        }
        copy {
            from bootJar
            into dockerBuildDir
        }
        copy {
            from project(':manifestmergetool').collect { it.tasks.withType(Jar) }
            into dockerBuildDir
        }
        copy {
            from 'docker'
            into dockerBuildDir
        }
    }
}

task buildDocker(type: DockerBuildImage) {
    dependsOn 'build', ':manifestmergetool:mainJar', 'dockerStageDir'
    inputDir = dockerBuildDir
    buildArgs = ['platform': 'linux/amd64']
    imageId = bootJar.archiveBaseName
    dockerFile = new File(dockerBuildDir, 'Dockerfile')
    images.add("extender/extender:latest")
    images.add("extender/extender:" + project.outSha)
}

task buildStandalone() {
    dependsOn 'build', ':manifestmergetool:mainJar'
    doLast {
        copy {
            from(project.projectDir) {
                include "users/**"
            }
            into "build/standalone"
        }
        copy {
            from("docker-base") {
                include "template.*"
            }
            into "build/standalone"
        }
        copy {
            from bootJar
            into "build/standalone"
        }
        copy {
            from project(':manifestmergetool').collect { it.tasks.withType(Jar) }
            into "build/standalone"
        }
    }
}
