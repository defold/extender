buildscript {
    repositories {
        mavenCentral()
    }
}

apply plugin: 'java-library'

ext {
    manifestMergetoolVersion = findProperty('manifestMergetoolVersion') ?: '0.5.0'
}

jar {
    archiveBaseName = '_manifestmergetool'
    enabled = true
    archiveClassifier = '' // use empty string to avoid the 'plain' suffix
}


configurations {
    // A configuration meant for consumers that need the API of this component
    implementation {
        canBeResolved = true
    }
}

task mainJar(type: Jar) {
    version =  project.ext.manifestMergetoolVersion
    archiveBaseName = 'manifestmergetool'
    manifest {
        attributes 'Main-Class': 'com.defold.manifestmergetool.ManifestMergeTool'
    }
    from {
        configurations.implementation.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    duplicatesStrategy DuplicatesStrategy.WARN
    with jar
        doLast {
        copy {
            from "${project.projectDir}/build/libs/manifestmergetool-${project.ext.manifestMergetoolVersion}.jar"
            into "app"
            rename { String fileName ->
                fileName.replace("-$project.ext.manifestMergetoolVersion", "")
            }
        }
    }
}

publishing {
  publications {
    manifestMergeTool(MavenPublication) {
      artifact mainJar
      groupId 'com.defold.extender'
      artifactId 'manifestmergetool'
      version project.ext.manifestMergetoolVersion
    }
  }
}

repositories {
    mavenCentral()
    maven { url "https://maven.google.com" }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    implementation('org.jsoup:jsoup:1.11.3')
    implementation('com.android.tools.build:manifest-merger:27.2.0')
    implementation('org.apache.commons:commons-configuration2:2.8.0')

    testImplementation('commons-io:commons-io:2.11.0')
    testImplementation('junit:junit:4.13')
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}
